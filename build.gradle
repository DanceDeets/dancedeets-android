
buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.13.+'
        classpath 'org.robolectric:robolectric-gradle-plugin:0.13.+'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        jcenter()
    }
}

apply plugin: 'com.android.application'
apply plugin: 'robolectric'

android {
    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
    }

    compileSdkVersion 18
    buildToolsVersion "20.0.0"

    defaultConfig {
        applicationId "com.dancedeets.dancedeets"
        minSdkVersion 15
        targetSdkVersion 18
        versionCode 1
        versionName "1.0"

        testApplicationId "com.dancedeets.dancedeets.tests"
        //testInstrumentationRunner "android.test.InstrumentationTestRunner"
        testInstrumentationRunner "com.google.android.apps.common.testing.testrunner.GoogleInstrumentationTestRunner"
    }
    buildTypes {
        release {
            runProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

repositories {
    mavenCentral()
}

robolectric {
    include '**/*Test.class'
    exclude '**/espresso/**/*.class'
    jvmArgs '-XX:-UseSplitVerifier'

}

dependencies {
    // For Parse...
    compile fileTree(dir: 'libs', include: ['*.jar'])

    // You must install or update the Support Repository through the SDK manager to use this dependency.
    compile 'com.android.support:support-v4:20.0.0'
    compile 'com.android.support:support-v13:20.0.0'
    // Don't want version 5.2.08, as it's not yet available on devices
    compile 'com.google.android.gms:play-services:5.0.89'
    compile 'com.mcxiaoke.volley:library:1.0.6'
    compile 'it.sephiroth.android.library.imagezoom:imagezoom:2.1.0'
    compile files('libs/Parse-1.7.0.jar')

    // Espresso
    androidTestCompile files('lib/espresso-1.1.jar', 'lib/testrunner-1.1.jar', 'lib/testrunner-runtime-1.1.jar')
    androidTestCompile 'com.google.guava:guava:14.0.1'
    androidTestCompile 'com.squareup.dagger:dagger:1.1.0'
    androidTestCompile 'org.hamcrest:hamcrest-integration:1.1'
    androidTestCompile 'org.hamcrest:hamcrest-core:1.1'
    androidTestCompile 'org.hamcrest:hamcrest-library:1.1'

    androidTestCompile('junit:junit:4.11') {
        exclude module: 'hamcrest-core'
    }
    androidTestCompile('org.robolectric:robolectric:2.3') {
        exclude module: 'classworlds'
        exclude module: 'commons-logging'
        exclude module: 'httpclient'
        exclude module: 'maven-artifact'
        exclude module: 'maven-artifact-manager'
        exclude module: 'maven-error-diagnostics'
        exclude module: 'maven-model'
        exclude module: 'maven-project'
        exclude module: 'maven-settings'
        exclude module: 'plexus-container-default'
        exclude module: 'plexus-interpolation'
        exclude module: 'plexus-utils'
        exclude module: 'wagon-file'
        exclude module: 'wagon-http-lightweight'
        exclude module: 'wagon-provider-api'
    }
    androidTestCompile 'com.squareup:fest-android:1.0.+'

    // These dependencies are necessary for robolectric to run its tests.
    // And since it runs outside of the android-test-framework as regular tests,
    // we need to list them manually here, or copy them over, as mentioned in:
    // http://bytesdonthurt.blogspot.com/2014/01/getting-started-with-robolectric-and.html
    testCompile 'junit:junit:4.11'
    testCompile 'org.robolectric:robolectric:2.3'
    testCompile fileTree(dir: 'libs/test', include: '*.jar')
    testCompile 'com.squareup:fest-android:1.0.+'
    testCompile 'com.google.android:android:4.1.1.4'
}


apply plugin: 'idea'

idea {
    module {
        testOutputDir = file('build/test-classes/debug')
    }
}
